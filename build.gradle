// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.versions)
}

subprojects {
    afterEvaluate {
        if (project.plugins.hasPlugin('com.android.library')) {
            android {
                compileSdk libs.versions.compileSdk.get() as Integer

                defaultConfig {
                    minSdk libs.versions.minSdk.get() as Integer
                    targetSdk libs.versions.targetSdk.get() as Integer
                    versionCode libs.versions.versionCode.get() as Integer
                    versionName libs.versions.versionName.get()
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    vectorDrawables.useSupportLibrary true
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_17
                    targetCompatibility = JavaVersion.VERSION_17
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17.toString()
                }
            }
        }
    }
}

tasks.named("dependencyUpdates").configure {
    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA']
                .any { keyword -> version.toUpperCase().contains(keyword) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }

    rejectVersionIf {
        !it.currentVersion.contains(".") ||
                (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion))
    }
}
